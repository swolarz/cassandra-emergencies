plugins {
	id 'java'
	id 'application'

	id 'idea'

	id 'org.springframework.boot' version '2.2.4.RELEASE'
	id 'io.spring.dependency-management' version '1.0.8.RELEASE'

	id 'com.google.protobuf' version '0.8.8'
}

group = 'pl.put.srds'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

def grpcVersion = '1.26.0' // CURRENT_GRPC_VERSION
def protobufVersion = '3.11.0'
def protocVersion = protobufVersion

repositories {
	maven {
		url "https://maven-central.storage-download.googleapis.com/repos/central/data/" }
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-data-cassandra'

	testImplementation('org.springframework.boot:spring-boot-starter-test') {
		exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
	}
	testImplementation 'io.projectreactor:reactor-test'

	implementation "com.google.protobuf:protobuf-java-util:${protobufVersion}"
	implementation "io.grpc:grpc-protobuf:${grpcVersion}"
	implementation "io.grpc:grpc-stub:${grpcVersion}"
	runtimeOnly "io.grpc:grpc-netty-shaded:${grpcVersion}"
	testImplementation "io.grpc:grpc-testing:${grpcVersion}"

	implementation 'javax.annotation:javax.annotation-api:1.3.2'

	compileOnly 'org.projectlombok:lombok:1.18.10'
	annotationProcessor 'org.projectlombok:lombok:1.18.10'
}

test {
	useJUnitPlatform()
}

application {
	mainClassName = 'pl.put.srds.emergencies.EmergenciesApplication'
}

protobuf {
	protoc { artifact = "com.google.protobuf:protoc:${protocVersion}" }
	plugins {
		grpc { artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}" }
	}
	generateProtoTasks {
		all()*.plugins { grpc {} }
	}

	generatedFilesBaseDir = "${projectDir}/build/generated/source/proto/main"
}

idea {
	module {
		sourceDirs += file("${projectDir}/build/generated/source/proto/main/java")
		sourceDirs += file("${projectDir}/build/generated/source/proto/main/grpc")
	}
}
